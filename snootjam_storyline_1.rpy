label snootjam_storyline_1:

 scene black
 with None
 show car neutral
 with dissolve
 play music maltShopTheme
 show car anon
 A "-hey, it could have been worse."
 show car trish
 T "Oh fuck off."
 show car fang at carBump
 F "I mean, he’s got a point"
 show car trish
 T "Just because you’re fucking doesn’t mean you have to defend him, dude."
 show car neutral
 "Reed finishes taking a long drag on his half-finished joint."
 show car reed
 Re "What are we even defending right now?"
 show car fang
 F "Anon sprayed the path to the cooler down with cooking oil."
 show car reed
 Re "Like by the door?"
 show car fang at carBump
 F "Heh, yeah."
 show car neutral
 "Reed giggles."
 show car reed
 Re "So thats why–"
 show car trish
 T "Yes! That’s why!"
 show car anon
 A "Well Stella thought it was pretty funny."
 show car trish
 T "That walking gym bag whore-"
 show car reed at carBump
 Re "Woah-hey, she’s not here to defend herself. Wait was she the one that...y’know–"
 show car fang
 F "Yeah, right after Trish started throwing a tantrum."
 show car reed
 Re "Wait, so why did we get kicked out of the party?"
 show car anon
 A "We didn’t."
 show car fang
 F "That powerslide through the screen door was fucking cash to watch. Did anyone get a video?"
 show car trish
 T "Screw you guys."
 show car anon
 A "Trish if you just kept your mouth shut-"
 show car trish
 T "Did I ask you, skinnie!?"
 show car neutral at carBump

 menu:
  "Play if off":
   jump decision_1_pio
  "Keep quiet":
   jump decision_1_kq
  "I fucked your mom":
   jump decision_1_ifym
   
label decision_1_pio:
 show car anon
 A "It’s a prank, not a dick. Don't take it so hard."
 show car trish at carBump
 T "WHAT?"
 show car anon
 A "Besides I didn’t think you’d drop so harsh. How’s your head?"
 show car trish
 T "My head is the best in the state."
 show car fang
 F "Damnit Trish, you know that’s not what he meant."
 show car neutral
 "Reed snickers as I let out a cathartic laugh. Seems Trish has finally calmed down"
 show car trish
 T "Look- the tile took more damage than I did. Thank god. I would have gored you right then and there if I cracked a horn because of your stupid prank bullshit."
 show car fang
 F "We could always go back and crash that shit."
 show car anon at carBump
 A "Game."
 show car trish
 T "Nah. Those guys are lame anyway. Not a drop of alcohol… who does that?"
 show car reed
 Re "Mormons, usually. Gotta go to the catholic parties. Those guys roll DEEP before it even starts. Highly recommend it."
 show car neutral
 jump end_decision_1

label decision_1_kq:
 show car trish
 T "Thought as much."
 show car neutral at carBump
 "She turns to Fang."
 show car trish
 T "You two leave me out of your fucking prank war. Can’t you see you’re just enabling him? What’s next, laxatives? ARSON!?"
 show car anon
 A "It’s not off the table."
 show car neutral
 "Trish narrows her eyes at me through the mirror."
 show car trish
 T "Anon."
 show car anon
 A "Permission to speak your trigger-ness"
 show car trish
 T "Just pushing all the buttons you can find tonight?"
 show car anon
 A "Bet if I push a little harder, I can get steam to come out of your nose."
 show car trish at carBump
 T "Yeah, and a horn in the chest. You have insurance?"
 show car anon
 A "Do you?"
 show car trish
 T "No. So– Don’t act like I can go to a hospital, and I’ll do the same for you, dig?"
 show car anon
 A "Loud and clear."
 show car neutral
 "Silence"
 show car reed
 Re "Anyone want a hit?"
 show car fang
 F "I’ll take one. You two are giving me a fucking migraine with your mating calls."
 show car neutral
 "Everyone giggles."
 show car neutral at carBump
 jump end_decision_1

label decision_1_ifym:
 show car anon
 A "Your mom sure didn’t ask when she tongue punched my fart-box. Should have heard the sound I made."
 show car trish
 T "Still didn’t ask."
 show car neutral
 "Fang starts laughing."
 show car anon
 A "Or maybe the time she gave me an under-table foot job at the Dave-and-Busters, don’t tell her that I was thinking about Fang’s mom the whole time though."
 show car reed
 Re "Alright, Anon, that’s enough."
 show car neutral
 "Fang is dying of laughter."
 show car trish at carBump
 T "Oh, you wanna fucking go?"
 show car anon
 A "Lookie here, I got that exact DM from your grandmother last night."
 show car neutral
 "Fuming, Trish whips her head around and lets loose"
 stop music
 show car trish
 T "{sc=1}You cueball lookin’ beach ball looking ass skinnie prick.{/sc}{nw}"
 T "{sc=2}Lookin’ like Mr. Clean's downie brother.{/sc}{nw}"
 T "{sc=3}Trigga’s got that 19-head, you look like a sniper’s dream.{/sc}{nw}" 
 T "{sc=4}Big words coming from a man with a hundred trigga-bytes of memory.{/sc}{nw}" 
 T "{sc=5}Got beforehead, forehead, and afterhead.{/sc}{nw}"
 show car trish at carBump
 T "{sc=6}Trigga tryna talk hot when bro’s brain is fucking vertical.{/sc}{nw}" 
 T "{sc=7}Frontal globe lookin’ ass trigga.{/sc}{nw}"
 T "{sc=8}Don’t get me started on that toothpick you call your morning wood.{/sc}{nw}"
 T "{sc=9}It’s like God's shaft division was out of budget when they made you.{/sc}{nw}" 
 T "{sc=10}Trigga you were made on Friday at 4:59 PM.{/sc}{nw}"
 show car neutral at carBump
 "Trish gets lost in a trigga moment and devolves into an unintelligible rant."
 "Fang grabs trish by the thigh."
 "Trish stops in her place and looks at fang with rage in her eyes."
 show car fang
 F "Be nice, Trish, he’s got a lot on his mind."
 show car neutral
 "Trish, fuming to the glass."
 show car trish
 T "You’re right, he does have a lot on his mind."
 show car neutral
 "…"
 show car reed at carBump
 Re "You know what could be on all our minds?"
 show car neutral
 "Silence."
 "Anon stares at Reed expectantly."
 show car anon
 A "Whats that, bub?"
 show car neutral
 "Reed is all too eager to indulge."
 show car reed
 Re "I’ve been thinking a lot about Wienerschnitzel."
 play music maltShopTheme
 show car anon
 A "Oh my god, please tell me all about Wienerschnitzel."
 show car neutral at carBump
 "Reed goes into a conspiracy theory about money laundering. The rabbit hole goes very far down there, and Reed takes us all to the bottom. Despite my heavy investment, Trish cuts him off after the fifth minute."
 jump end_decision_1
 
label end_decision_1:
 "Trish slouches in her seat and sighs."
 show car trish
 T "Alright. Where we heading? I guess we've still got a few hours before Ripley sends the locusts."
 show car reed
 Re "Anon thought we'd go take a trip downtown and celebrate the holidays properly."
 show car fang
 F "What are you talking about?"
 show car anon
 A "Go urban exploring, there’s an exquisite shithole downtown I would love to show off! Absolute masterpiece of engineering. "
 show car trish
 T "You’re fucking with me, right? Where’s the fun in that?"
 show car anon
 A "Dead serious, people leave the strangest shit just lying around, not to mention it's where all the top graffiti is, and the best haunts feel like little time capsules of the past. Where's your childlike urge to enter places you don’t belong?"
 show car fang
 F "Mine fell off a cliff in 09’."
 show car anon at carBump
 A "Oh right…."
 show car trish
 T "Can we not, and say we did?"
 show car anon
 A "Oh come on, you're overlooking the best part of it all."
 show car neutral
 "Anon grins and picks up a case of Coors Banquet from the back of the van"
 show car anon
 A "Unlike when we were children, we can- as Reed so eloquently puts it- ‘roll deep’."
 show car reed
 Re "Highly recommended."
 show car neutral
 "The looks of skepticism were quickly wiped off their faces."
 show car fang
 F "Alright, Dweeb went nuclear, what’s the plan?"
 show car anon
 A "You want to tell them reed?"
 show car trish
 T "Pass me a road soda."
 show car neutral at carBump
 "Reed is quick on the draw, and hands a frosty beer to Trish. Feeling a bit sober, he grabs one for himself."
 show car reed
 Re "What's the limit to drive in this county?"
 show car trish
 T "For an 18 year old male, I think it's 0.00."
 show car reed
 Re "They really need to work on that."
 show car neutral
 "Reed cracks his brew."
 show car anon
 A "Reed, you gonna tell them the details or what."
 show car reed
 Re "Right."
 show car neutral
 "Reed sheathes his brew, takes a hit, and starts gesturing in a general direction."
 show car reed at carBump
 Re "We're headed straight into the heart of VB’s old industrial district. We’ll be welcomed by the rotting remains of what’s left of the manufacturing plant on the corner of Spring and Thompson."
 Re "A magnificent construct of rebar, brick, and concrete. The clientele that reside in this area are very nice and can be trusted."
 show car trish
 T "But why there? I bet there's a hundred dumps just like it around VB."
 show car reed
 Re "Well..."
 show car neutral
 "He grins and holds his phone under his maw on high like a flashlight"
 #[dim scene a bit except for reeds face or at least reed]
 show reedStory neutral
 show car neutral:
  matrixcolor BrightnessMatrix(-0.25)
 with Dissolve(2)
 #[cut out the music]
 stop music
 show reedStory speak
 Re "Many years ago, it was an empire of steel processing, even got into plane building during the great war. The family that ran it were geniuses of their craft, and had perfected the art of worker exploitation."
 show reedStory neutral
 "He sits up and leans over as he gets sucked into his tale"
 show reedStory speak
 Re "Once it finally came time to retire or refit the plant, they decided to keep its doors wide open until the place started really collapsing." 
 Re "The death rates of the underpaid workers broke some international records, and in ‘62 the government finally shut down the operation."
 Re "Rumors say ever since, the victims' spirits continued to wander the crumbling halls, seeking vengeance on those that wronged them."
 show reedStory neutral
 pause 0
 #[return bright levels to normal]
 show car neutral:
  matrixcolor BrightnessMatrix(0)
 with dissolve
 hide reedStory neutral
 "Reed puts away his phone and goes back to finishing his joint."
 show car trish at carBump
 T "You don't think it's actually haunted do you?"
 show car fang
 F "Nah, ghosts aren’t real, I’ve tried all the rituals."
 show car anon
 A "People have said they've seen weird stuff happen out there. Strange noises late at night, lights coming out from the windows, shadowy figures that'll disappear when you blink."
 show car fang
 F "Captain Carfe and his gang of retard squatters pulling copper."
 show car reed
 Re "Nah, the locals steer clear of Spring and Thompson."
 show car neutral
 "…"
 show car anon
 A "Sure…and even if we find one, we’ll just turn around and leave; I mean it's not like they're gonna follow us home. So, we game?"
 show car trish
 T "Do we have a gun?"
 show car reed
 Re "No, but I’m a 6 foot 2 raptor, and I don’t doubt Fang’s ability to kill us all."
 show car fang at carBump
 F "I mean, he’s got a point."
 show car neutral
 #[everything except anon dims out a bit]
 show anonThink
 show car neutral:
  matrixcolor BrightnessMatrix(-0.25)
 with Dissolve(2)
 #[return music but quiet]
 play music maltShopTheme volume 0.25
 "While the rest of the gang find something else to yap about, I slump back in my seat as I rehearse my greatest plan yet."
 "Trish may have been amused by the appetizer, but the real show was just beginning."
 "I've been planning this shit for months. The perfect location, the perfect date, and a perfect crowd to witness my masterpiece."
 "Reed was easy to bribe with a few well earned herbal remedies."
 #[thought bubble graphic appears, dot by dot, about 2 or 3 dots whatever looks good, then the bubble, the animations should fit inside the bubble.)
 show thought bubble1
 with dissolve
 show thought bubble2
 with dissolve
 show thought bubble3
 with dissolve
 show thought bubble4
 with dissolve
 #[pull up first 2 frame loop]
 show animation animation1
 "The stage was all set."
 #[pull up second 2 frame loop]
 show animation animation2
 "Step one, play it off legit. Slowly get them boozed up."
 #[pull up third 2 frame loop]
 show animation animation3
 "Step two, once they’re cooked, Reed and I slip away."
 #[pull up fourth 2 frame loop]
 show animation animation4
 "Step three, slather Reed in phosphorescent paint until he glows nuclear waste. Then suit him up."
 #[pull up fifth 2 frame loop]
 show animation animation5
 "The coup de grace, come back screaming, like I've practiced."
 #[pull up 6th 2 frame loop]
 show animation animation6
 "Reed follows close behind, and nails me in the back with the trick knife. The fake blood packs taped to my back pop. Pray that Fang gets scared."
 #[pull up 7th 2 frame loop]
 show animation animation7
 "If everything goes right, the chase begins."
 #[pull up 8th 2 frame loop]
 show animation animation8
 "Make sure to get it all on film."
 #[pull up 9th 2 frame loop]
 show animation animation9
 "Then, final objective: Survive."
 #[Cut music for next score and cut to black]
 stop music
 hide animation animation9
 hide car neutral
 hide thought bubble4
 hide anonThink
 window auto hide
 pause 1
 
 "It's not long before we finally arrive at our destination. We collect our goods and disembark from Reed's pussy wagon to take in the view."
 #[cut in outside van and ambient sounds]
 scene outsideVan
 play music outsideAmbience
 #[the gangs sprites all enter together stage left in neutral pose]
 show reed neutral:
  xcenter -0.5 yalign 0.1
  easein_cubic 1 xcenter 0.85  
  xzoom -1
 show trish neutral:
  xcenter -0.5 yalign 0.1
  easein_cubic 1 xcenter 0.6
  xzoom -1
 show fang neutral:
  xcenter -0.5 yalign 0.1
  easein_cubic 1 xcenter 0.4
 show anon neutral:
  xcenter -0.5 yalign 0.1
  easein_cubic 1 xcenter 0.2
 pause 1.1
 Re "Well this is the place!"
 F "Oh my god."
 #[anon switches to the thumbs up one]
 show anon happy
 with dissolve
 A "Hey, what it lacks in youthful good looks, it makes up for in character!"
 #[switch trish to considering]
 show trish considering
 with dissolve
 T "You really didn’t grow up in the projects, did ya?"
 #[Anon switches to grin]
 show anon grin
 with dissolve
 A "I’m from Rock Bottom."
 T " …Fair. "
 #[anon & trish expression back to neutral, all sprites exit stage right (maybe trish lags behind a little?)]
 show anon neutral
 show trish neutral
 with dissolve
 show reed neutral:
  xzoom 1
  easein_cubic 1 xcenter 1.5
 show fang neutral:
  easein_cubic 1 xcenter 1.5
 show anon neutral:
  easein_cubic 1 xcenter 1.5
 show trish neutral:
  pause 0.5
  xzoom 1
  pause 0.5
  xzoom -1
  pause 0.25
  xzoom 1
  pause 0.25
  easein_cubic 1 xcenter 1.5
 pause 1.5
 #[screen fades to black]
 scene black with dissolve
 pause 0.5
 "We approach the derelict old cinder block, It's a miracle a junk-heap like this is still standing. I reach over into Reeds backpack and pull out my camera."
 #[Fade in to front entrance pic, reed fang and trish are all already standing there, the camera canvas is active now, anon is permanently off screen while camera is in play.]
 scene outsideBuilding
 show fang neutral:
  xcenter 0.25 yalign 0.1
 show trish neutral:
  xcenter 0.50 yalign 0.1
 show reed neutral:
  xcenter 0.85  yalign 0.1 xzoom-1
 show camera
 show recording
 with dissolve
 F "What do you need that thing for?"
 "I can’t contain my smug grin."
 A "Making memories"
 #[trish switches to unimpressed]
 show trish unimpressed
 T "And record us committing a misdemeanor?"
 A "Fine, I’ll keep you out of the shot. But also, ghosts are supposed to show up on cameras! Seen it on TV."
 T "Oh, on the show that plays before Ancient Aliens?"
 A "Maybe."
 #[fang exits on the closest side]
 show fang neutral:
  xzoom -1
 with dissolve
 show fang neutral:
  easein_cubic 1 xcenter -0.5
 with None
 show reed neutral:
  linear 0.25 xcenter 0.70
 show trish unimpressed:
  linear 0.25 xcenter 0.35
 "Fang stifles a laugh and leans in, and whispers into my ear." 
 F "Level with me, dweeb. Do you actually believe in ghosts?"
 A "I’m just saying that I personally cannot take a mere mortal’s word on such a fantastical thing."
 F "You’re trying to get on TV, arent you?"
 A "Perchance."
 #[switch trish to considering]
 show trish considering
 with dissolve
 T "You can’t just say perchance."
 #[Switch reed to explanatory]
 show reed explanatory
 with dissolve
 Re "All set?"
 $ patience = 0
 jump decision_2
 
label decision_2:
 show reed neutral
 show trish neutral
 if patience >= 5:
  jump decision_2_p
 menu:
  "Yes":
   jump decision_2_yes
  "No":
   jump decision_2_no
 
label decision_2_yes: 
 A "Lubed and loaded."
 F "Cocked and Locked."
 T "Unfortunately."
 #[switch reed to happy]
 show reed happy
 with dissolve
 Re "Right on."
 #[cut to black except the camera]
 hide outsideBuilding
 hide fang
 hide trish
 hide reed
 with dissolve
 #[play door opening sounds]
 play sound doorOpening
 "Reed strains against the rusty iron slabs until they finally shriek in protest as they give against his incredible strength, he waves us in with a dorky bow and we take a moment to absorb our new surroundings."
 #[End branch, Begin section 2]
 jump goofy_investigation

label decision_2_no:
 A "…"
 Re "You need more time?"
 
 menu:
  "Yeah":
   jump decision_2_yeah
  "This reeks":
   jump decision_2_tr
   
label decision_2_yeah:
 $ patience += 1
 "You take a moment to prepare yourself."
 Re "Ready now?"
 #[return to Yes/No]
 jump decision_2

label decision_2_tr:
 A "You know what, Trish is right, this is stupid. Let’s go somewhere else."
 T "Finally, some sense."
 F "Alright, I know the perfect beach to go drink on!"
 Re "I hate sand."
 T "It’s coarse and gets everywhere."
 scene black with dissolve
 "Reed and Trish skipped the beach, but Fang and I didnt."
 "I got drunk and said something I shouldn't have, and Fang broke up with me."
 "After a very long story involving me joining the Navy and Fang becoming addicted to Carfetanyl, we made up, fixed our problems, and lived happily ever after as successful musicians."
 "The End?"
 "Ending 2 of 4: “The Infinite Cope.”"
 return

#[If it’s not too much trouble, If you loop No/yeah more than 5 times than fork to this dialogue instead of going back to Yes/No]
label decision_2_p:
 F "Are you just going to stand there and stare at the wall all night? Come on!"
 "Fang grabs my wrist and drags me towards the entrance"
 hide outsideBuilding
 hide fang
 hide trish
 hide reed
 with dissolve
 #[play door opening sounds]
 play sound doorOpening
 #(we can change this based on the picture we use)
 "Reed strains against the rusty iron slabs until they finally shriek in protest as they give against his incredible strength, he waves us in with a dorky bow and we take a moment to absorb our new surroundings."
 #[End branch, Begin section 2]
 jump goofy_investigation
 return